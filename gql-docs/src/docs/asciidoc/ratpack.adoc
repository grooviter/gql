== Ratpack

Ratpack http://ratpack.io is a set of Java libraries for building
scalable HTTP applications. You can use Ratpack to make a given GraphQL schema available through
HTTP.

=== Example

Here is a minimum Groovy working example of a GraphQL schema exposed
through HTTP thanks to Ratpack.

[source, groovy]
----
@Grapes([
  @Grab('io.ratpack:ratpack-groovy:1.5.1'),
  @Grab('org.slf4j:slf4j-simple:1.7.25'),
  @Grab('com.github.grooviter:gql-core:0.3.2'),
  @Grab('com.github.grooviter:gql-ratpack:0.3.2')
])
import static ratpack.groovy.Groovy.ratpack

import gql.DSL
import gql.ratpack.GraphQLModule
import gql.ratpack.GraphQLHandler
import gql.ratpack.GraphiQLHandler

def schema = DSL.schema { // <1>
    queries('Queries') {
      field('hello') {
        type GraphQLString
        staticValue 'GraphQL and Groovy!'
      }
    }
}

ratpack {
    bindings {
      module GraphQLModule // <2>

      bindInstance schema // <3>
    }
    handlers {
        post('graphql', GraphQLHandler) // <4>
        get('graphql/browser', GraphiQLHandler) // <5>
    }
}
----
<1> Create the schema using `gql.DSL` api
<2> Add the `gql.ratpack.GraphQLModule` in order to provide sane defaults to handlers and GraphiQL configuration
<3> Add the schema to Ratpack's registry
<4> Register the `GraphQLHandler` to handle all GrahpQL request at `/graphql`
<5> Register the `GraphiQLHandler` to be able to expose GraphiQL client at `/graphql/browser`.

[sidebar]
.You can use `gql-ratpack` without GQL core
****
**gql-ratpack** is just a **Java** library exposing a GraphQL engine using Ratpack, it doesn't
have neither dependencies to GQL core nor to Groovy. That means that if you want to use Ratpack but maybe you
would like to use another library based on `graphql-java` (or the `graphql-java` library itself)
you only have to expose the handlers and then create the `graphql.Schema` instance the way you want.
****

=== Execution context

`GQL` Ratpack integration uses the Ratpack's handle context as the
GraphQL execution context. That enables to access Ratpack's context from
a data fetcher.

[source, groovy]
----
import ratpack.handling.Context
import graphql.schema.DataFetchingEnvironment

def schema = DSL.schema {
    queries('Queries') {
      field('hello') {
        type GraphQLString
        dataFetcher { DataFetchingEnvironment env ->
          Context context = env.context as Context

          return context
            .header('Authorization')       // <1>
            .map { 'GraphQL and Groovy' }  // <2>
            .orElse 'Unauthorizated'       // <3>
        }
      }
    }
}
----

<1> Takes content from header `Authorization`
<2> If it had a value it returns a positive message
<3> Otherwise informs the user is not authorized

Having the possibility of accessing Ratpack's context could be useful
for things like:

- Authentication
- Authorization
- Logging
- ...

You could also be able to access the context through `instrumentation`.

include::ratpack/errors.adoc[]

=== Instrumentation

Following `graphql-java` documentation The
graphql.execution.instrumentation.Instrumentation interface allows you
to inject code that can observe the execution of a query and also
change the runtime behaviour.

The `gql-ratpack` makes possible to add a given instance of type
`graphql.execution.instrumentation.Instrumentation` to the registry
and that instance will be used by the current execution.

If you'd like to use more than one instrumentation then you may create
an instance of
`graphql.execution.instrumentation.ChainedInstrumentation` and add all
other instrumentation instances to it, context will be passed through
the chained instrumentation to all children.

=== Configuration

If you would like to disable the GraphiQL client, you can always configure the
GraphQLModule setting the `activateGraphiQL` to `false`.

[source, groovy]
----
ratpack {
    bindings {
      module(GraphQLModule) { conf ->
        conf.activateGraphiQL = false // <1>
      }
      //...
    }
    handlers {
     //...
    }
}
----

<1> Setting the `activateGraphiQL` to false will disable GraphiQL client

[sidebar]
.GraphiQL Client limitation
****
At the moment, the GraphQL module provides GraphiQL as an static html
page. However the resources (scripts/css) required to make it work
properly aren't loaded from Ratpack but retrieved online once it's
loaded by the browser.

That means the GraphiQL client will only work if the browser accessing
it has online access.

Of course, the idea for upcoming releases would be to serve everything
from the module so that it could be used under any circumstances.
****

=== Futures.async/blocking

Ratpack's execution model requires that all blocking operations
are done in the blocking executor. Because one of the natural return types
of a data fetcher could be a CompletableFuture, we could use that type
as long as it is executed in the right executor.

To make this easier, from version `0.3.1` there is `gql.ratpack.exec.Futures`
which creates a blocking CompletableFuture instances `Futures.blocking` or
non blocking `Futures.async` using Ratpack's correct executors.

[source, groovy]
----
import ratpack.handling.Context
import graphql.schema.DataFetchingEnvironment
import gql.ratpack.exec.Futures

def schema = DSL.schema {
    queries('Queries') {
      field('hello') {
        type GraphQLString
        dataFetcher { DataFetchingEnvironment env ->
             Futures.blocking {
                // do something in the blocking executor
             }
        }
      }
    }
}
----

=== Pac4j

Although you can create your own `authentication` mechanisms
using instrumentations, it's also true that you can use already existent HTTP
authentication mechanisms. A good example could be Pac4j integration with
Ratpack.

[source, groovy]
----
@Grapes([
  @Grab('io.ratpack:ratpack-groovy:1.5.1'),
  @Grab('org.slf4j:slf4j-simple:1.7.25'),
  @Grab('com.github.grooviter:gql-core:0.3.2'),
  @Grab('com.github.grooviter:gql-ratpack:0.3.2')
])
import static ratpack.groovy.Groovy.ratpack

import gql.DSL
import gql.ratpack.GraphQLModule
import gql.ratpack.pac4j.GraphQLHandler

def schema = DSL.schema { // <1>
    queries('Queries') {
      field('hello') {
        type GraphQLString
        fetcher { env ->
          Context ctx = env.context as Context
          Optional<UserProfile> profile = Optional
            .ofNullable(ctx.request.get(UserProfile))

          return 'Grettings mate, you ${profile.isEmpty() ? 'dont' : 'do have'} credentials'
        }
      }
    }
}

ratpack {
    bindings {
      module GraphQLModule
      bindInstance schema
    }
    handlers {
        post('graphql', GraphQLHandler)
    }
}
----

=== Versioning alignment

.Version alignment
|===
|gql-ratpack | graphql-java | Ratpack | Groovy

|0.3.x | 7.0 | 1.5.1 | 2.4.x
|===
