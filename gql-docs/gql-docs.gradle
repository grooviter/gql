plugins {
  id("org.asciidoctor.jvm.convert") version "2.4.0"
  id('org.ajoberstar.git-publish') version "3.0.1"
}

task generateDocumentation() {
  dependsOn ':gql-core:groovydoc',':gql-ratpack:javadoc', asciidoctor

  doLast {
    copy {
      from "src/docs/site"
      into "$buildDir/site"
    }
  }
}

task setAuthentication() {
  doLast {
    System.setProperty('org.ajoberstar.grgit.auth.username', findProperty('PUBLISH_GH_TOKEN'))
  }
}

gitPublish {
  repoUri = 'https://github.com/grooviter/gql.git'
  branch = 'gh-pages'
  commitMessage = "Released version ${releaseVersion}"

  contents {
    from file('build/site')
  }
}

gitPublishPush {
  dependsOn 'setAuthentication'
  dependsOn 'generateDocumentation'
}

asciidoctor {
  sourceDir 'src/docs/asciidoc'
  outputDir file("$buildDir/site/docs")

  sources {
    include 'index.adoc'
  }

  attributes  'endpoint-url'       : 'http://github.com/grooviter/gql',
              'source-highlighter' : 'coderay',
              'releaseVersion'     : releaseVersion,
              'imagesdir'          : './images',
              'toc'                : 'left',
              'icons'              : 'font',
              'toclevels'          : 3
}

asciidoctorj {
  modules {
    diagram.version '1.5.4.1'
  }
}
