plugins {
  id 'groovy'
  id 'jacoco'
  id 'maven-publish'
  id 'signing'
}

dependencies {
  implementation "com.graphql-java:graphql-java:$graphql_java"
  implementation "io.ratpack:ratpack-core:$ratpack"
  implementation "io.ratpack:ratpack-guice:$ratpack"
  implementation "io.ratpack:ratpack-pac4j:$ratpack"

  testImplementation project(':gql-core')
  testImplementation "io.ratpack:ratpack-pac4j:$ratpack"
  testImplementation "io.ratpack:ratpack-groovy-test:$ratpack"

  testImplementation "org.spockframework:spock-core:$spock"
  testImplementation 'org.objenesis:objenesis:2.1'
}

compileGroovy {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

publishing {
  publications {
    'gql-ratpack'(MavenPublication) {
      groupId    "$releaseGroup"
      artifactId 'gql-ratpack'
      version    "$releaseVersion"

      from components.java
      artifact sourcesJar

      pom {
        name = "gql-core"
        description = "GQL Ratpack integration Release"
        url = "http://grooviter.github.io/gql/"
        licenses {
          license {
            name = "Apache-2.0"
            url = "https://www.apache.org/licenses/LICENSE-2.0"
          }
        }
        scm {
          connection = "https://github.com/grooviter/gql.git"
          developerConnection = "https://github.com/grooviter/gql.git"
          url = "https://github.com/grooviter/gql.git"
        }
      }
    }
  }
}

signing {
  required(System.getenv("PUBLISH_ENABLED"))
  sign(publishing.publications.'gql-ratpack')
  useInMemoryPgpKeys(
    System.getenv("PUBLISH_KEY_ID"),
    System.getenv("PUBLISH_KEY"),
    System.getenv("PUBLISH_SECRET"),
  )
}

