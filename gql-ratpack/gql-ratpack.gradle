apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

dependencies {
  implementation "com.graphql-java:graphql-java:$graphql_java"
  implementation "io.ratpack:ratpack-core:$ratpack"
  implementation "io.ratpack:ratpack-guice:$ratpack"
  implementation "io.ratpack:ratpack-pac4j:$ratpack"

  testImplementation project(':gql-core')
  testImplementation "io.ratpack:ratpack-pac4j:$ratpack"
  testImplementation "io.ratpack:ratpack-groovy-test:$ratpack"

  testImplementation "org.spockframework:spock-core:$spock"
  testImplementation 'org.objenesis:objenesis:2.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

publishing {
  publications {
    'gql-ratpack'(MavenPublication) {
      groupId    "$releaseGroup"
      artifactId 'gql-ratpack'
      version    "$releaseVersion"

      from components.java
      artifact sourcesJar
    }
  }
}

bintray {
  // resolves gradle.properties and environment variables
  user         = "${properties.bintrayUser ?: System.getenv('BINTRAY_USER') }"
  key          = "${properties.bintrayKey ?: System.getenv('BINTRAY_KEY') }"

  publish      = true
  publications = ['gql-ratpack']

  pkg {
    repo            = 'maven'
    name            = 'gql'
    userOrg         = 'grooviter'
    desc            =  "$releaseDescription"
    websiteUrl      = 'http://grooviter.github.io/gql/'
    issueTrackerUrl = 'https://github.com/grooviter/gql/issues'
    licenses        = ['Apache-2.0']
    vcsUrl          = 'https://github.com/grooviter/gql.git'
    labels          = ['groovy', 'graphql', 'dsl', 'asts']

    version {
      name      = "$releaseVersion"
      desc      = 'GQL Ratpack integration Release'
      released  = new Date()
      vcsTag    = "$releaseVersion"
    }
  }
}
