plugins {
  id 'org.kordamp.gradle.guide'
  id 'org.ajoberstar.git-publish'
}

task generateDocumentation() {
  dependsOn ':gql-core:groovydoc',':gql-ratpack:javadoc', "::aggregateGroovydoc", asciidoctor

  doLast {
    copy {
      from "$rootDir/build/docs/aggregate-groovydoc"
      into "$buildDir/site/api/"
    }
    copy {
      from "src/docs/site"
      into "$buildDir/site"
    }
  }
}

task setAuthentication() {
  doLast {
    System.setProperty('org.ajoberstar.grgit.auth.username', findProperty('PUBLISH_GH_TOKEN'))
  }
}

gitPublish {
  repoUri = 'https://github.com/grooviter/gql.git'
  branch = 'gh-pages'
  commitMessage = "Released version $version"

  contents {
    from file('build/site')
    duplicatesStrategy DuplicatesStrategy.INCLUDE
  }
}

gitPublishPush {
  dependsOn 'setAuthentication'
  dependsOn 'generateDocumentation'
}

asciidoctor {
  sourceDir 'src/docs/asciidoc'
  outputDir file("$buildDir/site/docs/html5")

  sources {
    include 'index.adoc'
  }

  attributes  'endpoint-url'       : 'http://github.com/grooviter/gql',
              'source-highlighter' : 'coderay',
              'releaseVersion'     : version,
              'imagesdir'          : './images',
              'toc'                : 'left',
              'icons'              : 'font',
              'toclevels'          : 3
}

asciidoctorj {
  modules {
    diagram.version '2.1.0'
  }
}
