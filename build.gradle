buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath "org.ajoberstar:gradle-git-publish:0.2.1"
    classpath 'org.asciidoctor:asciidoctorj-diagram:1.3.1'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
  }
}

plugins {
  id 'groovy'
  id 'org.asciidoctor.convert' version '1.5.3'
}

apply from: "${projectDir}/gradle/bintray.gradle"
apply from: "${projectDir}/gradle/gh_pages.gradle"

repositories {
  jcenter()
}

dependencies {
  compile 'com.github.grooviter:asteroid:0.2.4'
  compile 'org.codehaus.groovy:groovy-all:2.4.10'
  compile 'com.graphql-java:graphql-java:2.4.0'

  testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
  testCompile 'junit:junit:4.12'
}

compileGroovy {
  groovyOptions.configurationScript = new File(projectDir, folder_compiler)
}

task generateDocumentation() {
  dependsOn groovydoc
  dependsOn asciidoctor

  doLast {
    copy {
      from "src/docs/site"
      into "$buildDir/site"
    }
  }
}

sourceSets {
  groovydoc {
    resources {
      srcDir folder_groovydoc
    }
  }
}

groovydoc {
  link 'https://docs.oracle.com/javase/8/docs/api/', 'java'
  link 'http://docs.groovy-lang.org/latest/html/api/', 'groovy', 'org.codehaus.groovy'
  link 'http://grooviter.github.io/asteroid/javadocs', 'asteroid'
  link 'http://graphql-java.github.io/graphql-java', 'graphql'

  destinationDir = file("$buildDir/site/api")
  classpath = sourceSets.groovydoc.output + classpath
}

asciidoctor {
  requires 'asciidoctor-diagram'

  outputDir file("$buildDir/site/docs")
	attributes 'endpoint-url': 'http://github.com/grooviter/gql',
             'source-highlighter': 'coderay',
             'releaseVersion': releaseVersion,
             'imagesdir': './images',
             'toc': 'left',
             'icons': 'font',
             'toclevels': 3
}

