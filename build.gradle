buildscript {
  dependencies {
    classpath 'org.asciidoctor:asciidoctorj-diagram:1.3.1'
  }
}

plugins {
  id 'jacoco'
  id 'groovy'
  id 'codenarc'
  id 'org.asciidoctor.convert' version '1.5.3'
}

repositories {
  jcenter()
}

dependencies {
  compile 'com.github.grooviter:asteroid:0.2.4'
  compile 'org.codehaus.groovy:groovy-all:2.4.10'
  compile 'com.graphql-java:graphql-java:2.3.0'

  testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
  testCompile 'junit:junit:4.12'
}

sourceSets {
  groovydoc {
    resources {
      srcDir folder_groovydoc
    }
  }
}

compileGroovy {
  groovyOptions.configurationScript = new File(projectDir, folder_compiler)
}

codenarc {
    configFile = new File(projectDir, folder_codenarc)
    ignoreFailures = false
    reportFormat = codenarc_format
}

groovydoc {
  classpath = sourceSets.groovydoc.output + classpath
}

asciidoctor {
  requires 'asciidoctor-diagram'

	attributes 'endpoint-url': 'http://github.com/grooviter/gql',
             'source-highlighter': 'coderay',
             'imagesdir': './images',
             'toc': 'left',
             'icons': 'font',
             'toclevels': 3
}
