plugins {
  id 'groovy'
  id 'maven-publish'
}

apply plugin: 'com.jfrog.bintray'
apply from: "../gradle/quality.gradle"
apply from: "../gradle/groovydoc.gradle"

dependencies {
  compile "com.graphql-java:graphql-java:${graphql_java}"
  compile "org.codehaus.groovy:groovy-all:$groovy"

  testCompile "org.spockframework:spock-core:$spock"
  testCompile 'junit:junit:4.12'
}

compileGroovy {
  groovyOptions.configurationScript = new File(projectDir, folder_compiler)
}

sourceSets {
  groovydoc {
    resources {
      srcDir folder_groovydoc
    }
  }
}

groovydoc {
  link 'https://docs.oracle.com/javase/8/docs/api/', 'java'
  link 'http://docs.groovy-lang.org/latest/html/api/', 'groovy', 'org.codehaus.groovy'
  link 'http://graphql-java.github.io/graphql-java', 'graphql'
  docTitle project.name

  destinationDir = file("../gql-docs/build/site/api")
  classpath = sourceSets.groovydoc.output + classpath
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

publishing {
  publications {
    'gql-core'(MavenPublication) {
      groupId    "$releaseGroup"
      artifactId 'gql-core'
      version    "$releaseVersion"

      from components.java
      artifact sourcesJar
    }
  }
}

bintray {
  user         = "$bintrayUser"
  key          = "$bintrayKey"

  publish      = true
  publications = ['gql-core']

  pkg {
    repo            = 'maven'
    name            = 'gql'
    userOrg         = 'grooviter'
    desc            =  "$releaseDescription"
    websiteUrl      = 'http://grooviter.github.io/gql/'
    issueTrackerUrl = 'https://github.com/grooviter/gql/issues'
    licenses        = ['Apache-2.0']
    vcsUrl          = 'https://github.com/grooviter/gql.git'
    labels          = ['groovy', 'graphql', 'dsl', 'asts']

    version {
      name      = "$releaseVersion"
      desc      = 'GQL Release'
      released  = new Date()
      vcsTag    = "$releaseVersion"
    }
  }
}
