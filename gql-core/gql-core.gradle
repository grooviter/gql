plugins {
  id 'groovy'
  id 'java-library'
  id 'maven-publish'
  id 'signing'
}

apply from: "../gradle/quality.gradle"

dependencies {
  api "com.graphql-java:graphql-java:${graphql_java}"
  api "org.codehaus.groovy:groovy:$groovy"

  testImplementation "org.spockframework:spock-core:$spock"
  testImplementation 'junit:junit:4.12'
  testImplementation "org.codehaus.groovy:groovy:$groovy"
  testImplementation "org.codehaus.groovy:groovy-json:$groovy"
  testImplementation "org.codehaus.groovy:groovy-test:$groovy"
  testImplementation "org.codehaus.groovy:groovy-dateutil:$groovy"
}

compileGroovy {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
  groovyOptions.configurationScript = new File(projectDir, folder_compiler)
}

sourceSets {
  groovydoc {
    resources {
      srcDir folder_groovydoc
    }
  }
}

groovydoc {
  link 'https://docs.oracle.com/javase/8/docs/api/', 'java'
  link 'http://docs.groovy-lang.org/latest/html/api/', 'groovy', 'org.codehaus.groovy'
  link 'http://graphql-java.github.io/graphql-java', 'graphql'

  docTitle = project.name
  destinationDir = file("../gql-docs/build/site/api")
  classpath = sourceSets.groovydoc.output + classpath
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  classifier 'javadoc' // must use javadoc classifier to be able to deploy to Sonatype
  from groovydoc.destinationDir
}

publishing {
  repositories {
    maven {
      name = releaseVersion.endsWith('SNAPSHOT') ? 'snapshots' : 'releases'
      url  = releaseVersion.endsWith('SNAPSHOT')
        ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
      credentials {
        username(findProperty("PUBLISH_REPO_USERNAME"))
        password(findProperty("PUBLISH_REPO_PASSWORD"))
      }
    }
  }
  publications {
    'gql-core'(MavenPublication) {
      groupId    "$releaseGroup"
      artifactId 'gql-core'
      version    "$releaseVersion"
      from       components.java
      artifact   sourcesJar

      artifact(groovydocJar) {
        classifier = 'javadoc'
      }
      pom {
        name = "gql-core"
        description = "GQL core Release"
        url = "http://grooviter.github.io/gql/"
        licenses {
          license {
            name = "Apache-2.0"
            url = "https://www.apache.org/licenses/LICENSE-2.0"
          }
        }
        developers {
          developer {
            id = 'mario'
            name = 'Mario Garcia'
          }
        }
        scm {
          connection = "https://github.com/grooviter/gql.git"
          developerConnection = "https://github.com/grooviter/gql.git"
          url = "https://github.com/grooviter/gql.git"
        }
      }
    }
  }
}

signing {
  sign(publishing.publications.'gql-core')
  required(findProperty("PUBLISH_SIGN_ENABLED"))
  useInMemoryPgpKeys(
    findProperty("PUBLISH_SIGN_KEY_ID"),
    findProperty("PUBLISH_SIGN_KEY"),
    findProperty("PUBLISH_SIGN_SECRET")
  )
}
